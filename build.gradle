buildscript {
    apply from: 'build-tools/repositories.gradle'

    ext {
        opensearch_version = System.getProperty("opensearch.version", "1.2.4-SNAPSHOT")
        // 1.0.0 -> 1.0.0.0, and 1.0.0-SNAPSHOT -> 1.0.0.0-SNAPSHOT
        opensearch_build = opensearch_version.replaceAll(/(\.\d)([^\d]*)$/, '$1.0$2')
        common_utils_version = System.getProperty("common_utils.version", opensearch_build)
        kotlin_version = '1.3.72'
    }

    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

import org.opensearch.gradle.test.RestIntegTestTask
import org.opensearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'opensearch.opensearchplugin'

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}

opensearchplugin {
    name 'opensearch-hello-world'
    description 'Amazon OpenSearch Hello World Plugin'
    classname 'org.opensearch.rest.HelloWorldPlugin'
}

configurations.testCompile {
    exclude module: "securemock"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    compile "org.jetbrains:annotations:13.0"

    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
}

licenseHeaders.enabled = true
dependencyLicenses.enabled = false
validateNebulaPom.enabled = false
thirdPartyAudit.enabled = false
loggerUsageCheck.enabled = false

test {
    systemProperty 'tests.security.manager', 'false'
}